#+TITLE: Backlinks
* Backlinks
:PROPERTIES:
:ID:       3eadd5d9-cf67-47d5-ae6b-6b6e213dcf95
:END:

It can be useful to search for all notes that link to the current note. You can use =org-occur-link-in-agenda-files= to search for backlinks to the current node.

Unfortunately, out of the box this requires that the link description exactly match the title of the node. Here is an alternative solution that searchs for any backlink, no matter the description.

#+begin_src emacs-lisp :results silent
(defun zorg-backlinks ()
  "Search for backlinks to current entry."
  (interactive)
  (let ((link (condition-case nil
		  (org-id-store-link)
		(error "Unable to create a link to here"))))
    (org-occur-in-agenda-files (regexp-quote link))))
#+end_src

If you use [[https://github.com/alphapapa/org-ql][org-ql]] you might prefer this:

#+begin_src emacs-lisp :results silent
(defun zorg-backlinks ()
  (interactive)
  (let* ((link (condition-case nil
		  (org-id-store-link)
	       (error "Unable to create a link to here")))
         (query `(link :target ,link)))
    (org-ql-search
      org-agenda-text-search-extra-files
      query
      :title (concat "Links to: " (org-get-heading t t)))))
#+end_src

If you use [[https://github.com/alphapapa/org-ql][org-ql]] and you want to maintain a [[https://orgmode.org/manual/Dynamic-Blocks.html][dynamic block]] of backlinks, you can use this:

#+begin_src emacs-lisp :results silent
(defun org-dblock-write:zorg-backlinks (&rest params)
  (let ((elements (org-ql-query
                    :from org-agenda-text-search-extra-files
                    :where `(link :target ,(org-id-store-link)))))
    (insert "Backlinks:\n")
    (dolist (element elements)
      (insert "- " (org-make-link-string (concat "id:" (org-element-property :ID element))
                                         (org-element-property :raw-value element))
              "\n"))
    (delete-char -1)))
#+end_src

You can then add dynamic blocks:

#+begin: zorg-backlinks
Backlinks:
- [[id:b9c8bf44-3980-4026-8737-cc546a166d31][Start here]]
#+end

These can be updated using =C-c C-c= on the block. For other options to keep the blocks up to date, see the [[https://orgmode.org/manual/Dynamic-Blocks.html][documentation]].
